/// <summary>Module for generating references to classes in the <c>System.Runtime</c> assembly.</summary>
[<RequireQualifiedAccess>]
module internal Wacil.Compiler.Emit.SystemLibrary

open AsmResolver.DotNet
open AsmResolver.DotNet.Signatures
open AsmResolver.DotNet.Signatures.Types

/// <summary>
/// Represents a reference to the <see cref="T:System.MulticastDelegate"/> class, which serves as the base type for
/// <see langword="delegate"/> types generated by most .NET compiler.
/// </summary>
[<RequireQualifiedAccess; NoComparison; NoEquality>]
type MulticastDelegateClass =
    { Type: ITypeDefOrRef
      Signature: TypeDefOrRefSignature }

[<NoComparison; NoEquality>]
type DelegateClass =
    { Type: ITypeDefOrRef
      GetTarget: IMethodDefOrRef
      GetMethod: IMethodDefOrRef
      Create: IMethodDefOrRef }

/// <summary>Represents a reference to the <see cref="T:System.Type"/> class.</summary>
[<NoComparison; NoEquality>]
type TypeClass =
    { Type: ITypeDefOrRef
      GetTypeFromHandle: IMethodDefOrRef }

[<NoComparison; NoEquality>]
type RuntimeHelpersClass =
    { InitalizeArray: IMethodDefOrRef }

[<NoComparison; NoEquality>]
type References =
    { /// <summary>
      /// Represents a reference to the <see cref="T:System.ValueType"/> class, which serves as the base type for all value types.
      /// </summary>
      ValueType: ITypeDefOrRef
      ObjectConstructor: IMethodDefOrRef
      Delegate: DelegateClass
      MulticastDelegate: MulticastDelegateClass
      Type: TypeClass
      ArgumentNullExceptionConstructor: IMethodDefOrRef
      RuntimeHelpers: RuntimeHelpersClass
      TargetFrameworkAttributeConstructor: ICustomAttributeType }

let importTypes (assembly: AssemblyReference) (mdle: ModuleDefinition) =
    let importCoreType = ImportHelpers.importType mdle.DefaultImporter assembly
    let importSystemType = importCoreType "System"

    let tyDelegate = importSystemType "Delegate"
    let tyMulticastDelegate = importSystemType "MulticastDelegate"
    let tyType = importSystemType "Type"
    let tyRuntimeHelpers = importCoreType "System.Runtime.CompilerServices" "RuntimeHelpers"

    let sigDelegate = TypeDefOrRefSignature tyDelegate
    let sigMethodInfo = TypeDefOrRefSignature(importCoreType "System.Reflection" "MethodInfo")
    let sigMulticastDelegate = TypeDefOrRefSignature tyMulticastDelegate
    let sigType = TypeDefOrRefSignature tyType

    { ValueType = importSystemType "ValueType"
      ObjectConstructor = ImportHelpers.importConstructor mdle Seq.empty mdle.CorLibTypeFactory.Object.Type
      ArgumentNullExceptionConstructor =
        ImportHelpers.importConstructor mdle [| mdle.CorLibTypeFactory.String |] (importSystemType "ArgumentNullException")
      Delegate =
        { Type = tyDelegate
          GetTarget =
            ImportHelpers.importPropertyAccessor
                mdle.DefaultImporter
                CallingConventionAttributes.HasThis
                mdle.CorLibTypeFactory.Object
                "get_Target"
                tyDelegate
          GetMethod =
            ImportHelpers.importPropertyAccessor
                mdle.DefaultImporter
                CallingConventionAttributes.HasThis
                sigMethodInfo
                "get_Method"
                tyDelegate
          Create =
            ImportHelpers.importMethod
                mdle.DefaultImporter
                CallingConventionAttributes.Default
                sigDelegate
                [|
                    sigDelegate
                    mdle.CorLibTypeFactory.Object
                    sigMethodInfo
                |]
                "CreateDelegate"
                tyDelegate }
      MulticastDelegate =
        { MulticastDelegateClass.Type = tyMulticastDelegate
          MulticastDelegateClass.Signature = sigMulticastDelegate }
      Type =
        { Type = tyType
          GetTypeFromHandle =
            ImportHelpers.importMethod
                mdle.DefaultImporter
                CallingConventionAttributes.Default
                sigType
                [| TypeDefOrRefSignature(importSystemType "RuntimeTypeHandle") |]
                "GetTypeFromHandle"
                tyType
          }
      RuntimeHelpers =
        { InitalizeArray =
            ImportHelpers.importMethod
                mdle.DefaultImporter
                CallingConventionAttributes.Default
                mdle.CorLibTypeFactory.Void
                [|
                    TypeDefOrRefSignature(importSystemType "Array")
                    TypeDefOrRefSignature(importSystemType "RuntimeFieldHandle")
                |]
                "InitializeArray"
                tyRuntimeHelpers }
      TargetFrameworkAttributeConstructor =
        importCoreType "System.Runtime.Versioning" "TargetFrameworkAttribute"
        |> ImportHelpers.importConstructor mdle [| mdle.CorLibTypeFactory.String |]
        :?> ICustomAttributeType }
